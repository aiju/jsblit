int
_nz(string size, int a)
{
	int b;
	
	b = _cast(size, a, true);
	_v = false;
	_z = b == 0;
	_n = b < 0;
	_c = false;
	return a;
}

int
_add(string size, int a, int b, int c, bool zflag)
{
	int r, t;
	var u, s;

	switch(size){
	case "B":
		u = _u8;
		s = _s8;
		break;
	case "W":
		u = _u16;
		s = _s16;
		break;
	case "L":
		u = _u32;
		s = _s32;
		break;
	default: throw "derp";
	}
	r = u(a) + u(b) + c;
	t = s(a) + s(b) + c;
	_z = (_z || zflag) && u(r) == 0;
	_c = _x = u(r) != r;
	_n = s(r) < 0;
	_v = s(t) != t;
	return u(r);
}

int
_sub(string size, int a, int b, int c, bool xflag, bool zflag)
{
	int r, t;
	var u, s;

	switch(size){
	case "B":
		u = _u8;
		s = _s8;
		break;
	case "W":
		u = _u16;
		s = _s16;
		break;
	case "L":
		u = _u32;
		s = _s32;
		break;
	default: throw "derp";
	}
	r = u(a) - u(b) - c;
	t = s(a) - s(b) - c;
	_z = (_z || zflag) && u(r) == 0;
	_c = u(r) != r;
	_n = s(r) < 0;
	_v = s(t) != t;
	if(xflag) _x = _c;
	return u(r);
}

int
_asl(string size, int a, int b)
{
	int i, n, r;

	switch(size){
	case "B": n = 8; break;
	case "W": n = 16; break;
	case "L": n = 32; break;
	}
	b &= 63;
	_c = false;
	_v = false;
	for(i = 0; i < b; i++){
		_x = _c = (a >> n-1 & 1) != 0;
		_v = _v || ((a << 1 ^ a) >> n-1 & 1) != 0;
		a <<= 1;
	}
	r = _cast(size, a, true);
	_z = r == 0;
	_n = r < 0;
	return r;
}

int
_asr(string size, int a, int b)
{
	int i, n, r;

	switch(size){
	case "B": n = 8; break;
	case "W": n = 16; break;
	case "L": n = 32; break;
	}
	b &= 63;
	a = _cast(size, a, true);
	_c = false;
	_v = false;
	for(i = 0; i < b; i++){
		_x = _c = (a & 1) != 0;
		_v = _v || ((a >> 1 ^ a) >> n-1 & 1) != 0;
		a >>= 1;
	}
	r = _cast(size, a, true);
	_z = r == 0;
	_n = r < 0;
	return r;
}

int
_lsl(string size, int a, int b)
{
	int i, n, r;

	switch(size){
	case "B": n = 8; break;
	case "W": n = 16; break;
	case "L": n = 32; break;
	}
	b &= 63;
	_c = false;
	_v = false;
	for(i = 0; i < b; i++){
		_x = _c = (a >> n-1 & 1) != 0;
		a <<= 1;
	}
	r = _cast(size, a, true);
	_z = r == 0;
	_n = r < 0;
	return r;
}

int
_lsr(string size, int a, int b)
{
	int i, n, r;

	switch(size){
	case "B": n = 8; break;
	case "W": n = 16; break;
	case "L": n = 32; break;
	}
	b &= 63;
	_c = false;
	_v = false;
	for(i = 0; i < b; i++){
		_x = _c = (a & 1) != 0;
		a >>>= 1;
	}
	r = _cast(size, a, true);
	_z = r == 0;
	_n = r < 0;
	return r;
}

int
_rol(string size, int a, int b)
{
	int i, n, m, r;

	switch(size){
	case "B": n = 8; break;
	case "W": n = 16; break;
	case "L": n = 32; break;
	}
	b &= 63;
	a = _cast(size, a, false);
	_c = false;
	_v = false;
	for(i = 0; i < b; i++){
		a = a << 1 | a >>> n-1 & 1;
		_c = (a & 1) != 0;
	}
	r = _cast(size, a, true);
	_z = r == 0;
	_n = r < 0;
	return r;
}

int
_ror(string size, int a, int b)
{
	int i, n, r;

	switch(size){
	case "B": n = 8; break;
	case "W": n = 16; break;
	case "L": n = 32; break;
	}
	b &= 63;
	a = _cast(size, a, false);
	_c = false;
	_v = false;
	for(i = 0; i < b; i++){
		_c = (a & 1) != 0;
		a = a >>> 1 | (a & 1) << n-1;
	}
	r = _cast(size, a, true);
	_z = r == 0;
	_n = r < 0;
	return r;
}

int
_roxl(string size, int a, int b)
{
	int i, n, m, r, x, y;

	switch(size){
	case "B": n = 8; break;
	case "W": n = 16; break;
	case "L": n = 32; break;
	}
	b &= 63;
	_c = false;
	_v = false;
	a = _cast(size, a, false);
	x = (int)_x;
	_c = _x;
	for(i = 0; i < b; i++){
		y = a >> n-1 & 1;
		_c = _x = y != 0;
		a = a << 1 | x;
		x = y;
	}
	r = _cast(size, a, true);
	_z = r == 0;
	_n = r < 0;
	return r;
}

int
_roxr(string size, int a, int b)
{
	int i, n, r, x, y;

	switch(size){
	case "B": n = 8; break;
	case "W": n = 16; break;
	case "L": n = 32; break;
	}
	b &= 63;
	a = _cast(size, a, false);
	_c = false;
	_v = false;
	a = _cast(size, a, false);
	x = (int)_x;
	_c = _x;
	for(i = 0; i < b; i++){
		y = a & 1;
		_c = _x = y != 0;
		a = a >>> 1 | x << n-1;
		x = y;
	}
	r = _cast(size, a, true);
	_z = r == 0;
	_n = r < 0;


	return r;
}

int
_abcd(int a, int b)
{
	int ss, bc, dc, corf, rr;
	
	ss = _u8(a + b + (int)_x);
	bc = (a & b | ~ss & a | ~ss & b) & 0x88;
	dc = ((ss + 0x66 ^ ss) & 0x110) >> 1;
	corf = _u8((bc | dc) - ((bc | dc) >> 2));
	rr = _u8(ss + corf);
	_x = _c = _s8(bc | ss & ~rr) < 0;
	_v = _s8(~ss & rr) < 0;
	_z = _z && rr == 0;
	_n = _s8(rr) < 0;
	return rr;
}

int
_sbcd(int a, int b)
{
	int dd, bc, dc, corf, rr;
	
	dd = _u8(a - b - (int)_x);
	bc = (~a & b | dd & ~a | dd & b) & 0x88;
	corf = _u8(bc - (bc >> 2));
	rr = _u8(dd - corf);
	_x = _c = _s8(bc | ~dd & rr) < 0;
	_v = _s8(dd & ~rr) < 0;
	_z = _z && rr == 0;
	_n = _s8(rr) < 0;
	return rr;
}


int
_nbcd(int xx)
{
	int dd, bc, corf, rr;
	
	dd = _u8(-xx - (int)_x);
	bc = (xx | dd) & 0x88;
	corf = bc - (bc >> 2);
	rr = u8(dd - corf);
	_x = _c = _s8(bc | ~dd & rr) < 0;
	_v = _s8(dd & ~rr) < 0;
	_z = _z && rr == 0;
	_n = _s8(rr) < 0;
	return rr;
}

int
_bop(string s, int a, int n, string op)
{
	int k;

	switch(s){
	case "B": n &= 7; break;
	case "L": n &= 31; break;
	default: throw "_bchg";
	}
	k = 1<<n;
	_z = (a & k) == 0;
	switch(op){
	case "bchg": return a ^ k;
	case "bclr": return a & ~k;
	case "bset": return a | k;
	case "btst": return a;
	}
}

int
_divs(int a, int b)
{
	int r, q;

	a = _s32(a);
	b = _s16(b);
	if(b == 0) throw "no";
	q = Math.trunc(a / b);
	r = a % b;
	if((r^a)<0) r = -r;
	if(_s16(q) != q){
		_c = false;
		_v = true;
		return _u32(a);
	}else{
		_nz("W", q);
		return u16(q) | u16(r) << 16;
	}
}

int
_divu(int a, int b)
{
	int r, q;

	a = _u32(a);
	b = _u16(b);
	if(b == 0) throw "no";
	q = Math.floor(a / b);
	r = a % b;
	if(_u16(q) != q){
		_c = false;
		_v = true;
		return _u32(a);
	}else{
		_nz("W", q);
		return u16(q) | u16(r) << 16;
	}
}

void
_movem_store(string s, var am, int l)
{
	string sa;
	int a, i;
	
	if(am[0] == "-(A)"){
		a = rval_addr(s, 8+am[1]);
		switch(s){
		case "W":
			for(i = 0; i < 16; i++)
				if((l>>i & 1) != 0){
					a -= 2;
					write("W", (string)_u32(a), rval(15-i));
				}
			break;
		case "L":
			for(i = 0; i < 16; i++)
				if((l>>i & 1) != 0){
					a -= 4;
					write("L", (string)_u32(a), rval(15-i));
				}
			break;
		}
		write("L", "R"+(string)(8+am[1]), a);
	}else{
		sa = _amode(s, am);
		a = (int)sa;
		switch(s){
		case "W":
			for(i = 0; i < 16; i++)
				if((l>>i & 1) != 0){
					write("W", (string)_u32(a), rval(i));
					a += 2;
				}
			break;
		case "L":
			for(i = 0; i < 16; i++)
				if((l>>i & 1) != 0){
					write("L", (string)_u32(a), rval(i));
					a += 4;
				}
			break;
		}
	}
}

void
_movem_load(string s, var am, int l)
{
	string sa;
	int a, i;
	
	if(am[0] == "(A)+")
		a = rval_addr(s, 8+am[1]);
	else{
		sa = _amode(s, am);
		a = (int)sa;
	}
	switch(s){
	case "W":
		for(i = 0; i < 16; i++)
			if((l>>i & 1) != 0){
				mem_sug("W", _u32(a), random(16));
				rend[i] = _u32(read("W", (string)_u32(a), true));
				a += 2;
			}
		break;
	case "L":
		for(i = 0; i < 16; i++)
			if((l>>i & 1) != 0){
				mem_sug("L", _u32(a), random(32));
				rend[i] = read("L", (string)_u32(a), false);
				a += 4;
			}
		break;
	}
	if(am[0] == "(A)+")
		write("L", "R"+(string)(8+am[1]), a);
}

bool
_checkcc(string s)
{
	switch(s){
	case "t": return true;
	case "f": return false;
	case "cc": return !_c;
	case "cs": return _c;
	case "vc": return !_v;
	case "vs": return _v;
	case "pl": return !_n;
	case "mi": return _n;
	case "ne": return !_z;
	case "eq": return _z;
	case "ge": return _n == _v;
	case "lt": return _n != _v;
	case "gt": return _n == _v && !_z;
	case "le": return _n != _v || _z;
	case "hi": return !(_c || _z);
	case "ls": return _c || _z;
	default: throw "_checkcc";
	}
}

string
format(var x[])
{
	switch(x[0]){
	case "add":
	case "abcd":
	case "adda":
	case "addi":
	case "addq":
	case "addx":
	case "and":
	case "andi":
	case "asl":
	case "asr":
	case "bchg":
	case "btst":
	case "bclr":
	case "bset":
	case "chk":
	case "cmp":
	case "cmpa":
	case "cmpi":
	case "cmpm":
	case "divs":
	case "divu":
	case "eor":
	case "eori":
	case "exg":
	case "lea":
	case "link":
	case "lsl":
	case "lsr":
	case "move":
	case "movea":
	case "movem":
	case "movep":
	case "moveq":
	case "muls":
	case "mulu":
	case "or":
	case "ori":
	case "rol":
	case "ror":
	case "roxl":
	case "roxr":
	case "sbcd":
	case "sub":
	case "subi":
	case "suba":
	case "subx":
	case "subq":
		return x[0].toUpperCase() + "." + x[1] + " " + formata(x[2]) + ", " + formata(x[3]);
	case "andi_ccr":
	case "clr":
	case "eori_ccr":
	case "ext":
	case "jmp":
	case "move_to_ccr":
	case "move_from_sr":
	case "nbcd":
	case "neg":
	case "negx":
	case "not":
	case "ori_ccr":
	case "pea":
	case "swap":
	case "tas":
	case "tst":
	case "unlk":
		return x[0].toUpperCase() + "." + x[1] + " " + formata(x[2]);
	case "bcc":
		return "B" + x[1].toUpperCase() + " " + formata(x[2]);
	case "dbcc":
		return "DB" + x[1].toUpperCase() + " " + formata(x[2]) + ", " + formata(x[3]);
	case "scc":
		return "S" + x[2].toUpperCase() + "." + x[1] + " " + formata(x[2]);
	case "bsr":
		return x[0].toUpperCase() + " " + formata(x[1]);
	case "nop":
	case "rtr":
	case "rts":
		return x[0].toUpperCase();
	default:
		throw "dunno how to format " + (string)x;
	}
}

void
_skipbra(var disp)
{
	if(disp[1] > 0x8000 || disp[1] <= -0x8000)
			pc += 4;
	else if(disp[1] == 0 || disp[1] == -1 || disp[1] < -0x80 || disp[1] >= 0x80)
			pc += 2;
}

void
simulate(var x[])
{
	string a, b, s;
	int m, n;

	rstart = new Array(16);
	rend = new Array(16);
	s = x[1];
	switch(x[0]){
	case "abcd":
		if(x[2][0] == "-(A)" && x[2][1] == x[3][1]) throw "no";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _abcd(read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "add":
	case "addi":
		if(x[3][0] == "A" && s == "B") throw "404";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _add(s, read(s, b, false), read(s, a, false), 0, true);
		write(s, b, m);
		return;
	case "adda":
		if(s == "B") throw "404";
		a = _amode(s, x[2]);
		b = _amode("L", x[3]);
		m = read("L", b, false) + read(s, a, true);
		write("L", b, m);
		return;
	case "addq":
		x[2][1] &= 7;
		if(x[2][1] == 0) x[2][1] = 8;
		if(x[3][0] == "A"){
			b = _amode("L", x[3]);
			m = read("L", b, false) + x[2][1];
			write("L", b, m);
		}else{
			b = _amode(s, x[3]);
			m = _add(s, read(s, b, false), x[2][1], 0, true);
			write(s, b, m);
		}
		return;
	case "addx":
		if(x[2][0] == "-(A)" && x[2][1] == x[3][1]) throw "no";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _add(s, read(s, b, false), read(s, a, false), (int)_x, false);
		write(s, b, m);
		return;
	case "and":
	case "andi":
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _nz(s, read(s, b, false) & read(s, a, false));
		write(s, b, m);
		return;
	case "andi_ccr":
		_setccr(_getccr() & x[2][1]);
		pc += 2;
		return;
	case "asl":
		a = _amode("B", x[2]);
		b = _amode(s, x[3]);
		m = _asl(s, read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "asr":
		a = _amode("B", x[2]);
		b = _amode(s, x[3]);
		m = _asr(s, read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "bcc":
		if(_checkcc(x[1]))
			pc = _u32(pc + x[2][1]);
		else
			_skipbra(x[2]);
		return;
	case "dbcc":
		if(!_checkcc(x[1])){
			a = _amode("W", x[2]);
			m = _s16(read("W", a, true) - 1);
			write("W", a, m);
			if(m != -1)
				pc = _u32(pc + _s16(x[3][1]));
			else
				pc += 2;
		}else
			pc += 2;
		return;
	case "bchg":
	case "btst":
	case "bclr":
	case "bset":
		a = _amode("B", x[2]);
		b = _amode(s, x[3]);
		m = _bop(s, read(s, b, false), read(s, a, false), x[0]);
		if(x[0] != "btst") write(s, b, m);
		return;
	case "bsr":
		rval_addr("L", 15);
		if(x[1][1] > 0x8000 || x[1][1] <= -0x8000)
			m = pc + 4;
		else if(x[1][1] == 0 || x[1][1] == -1 || x[1][1] < -0x80 || x[1][1] >= 0x80)
			m = pc + 2;
		else
			m = pc;
		write("L", "R15", read("L", "R15", false) - 4);
		write("L", (string)read("L", "R15", false), m);
		pc = _u32(pc + x[1][1]);
		return;
	case "clr":
		a = _amode(s, x[2]);
		write(s, a, 0);
		_n = _v = _c = false;
		_z = true;
		return;
	case "cmp":
	case "cmpi":
		if(x[3][0] == "A" && s == "B") throw "404";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		_sub(s, read(s, b, false), read(s, a, false), 0, false, true);
		return;
	case "cmpa":
		if(s == "B") throw "404";
		a = _amode(s, x[2]);
		b = _amode("L", x[3]);
		_sub("L", read("L", b, false), read(s, a, true), 0, false, true);
		return;
	case "cmpm":
		if(x[2][1] == x[3][1]) throw "no";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		_sub(s, read(s, b, false), read(s, a, false), 0, false, true);
		return;
	case "divs":
		a = _amode("W", x[2]);
		b = _amode("L", x[3]);
		m = _divs(read("L", b, false), read("W", a, false));
		write("L", b, m);
		return;
	case "divu":
		a = _amode("W", x[2]);
		b = _amode("L", x[3]);
		m = _divu(read("L", b, false), read("W", a, false));
		write("L", b, m);
		return;
	case "eor":
	case "eori":
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _nz(s, read(s, b, false) ^ read(s, a, false));
		write(s, b, m);
		return;
	case "eori_ccr":
		_setccr(_getccr() ^ x[2][1]);
		pc += 2;
		return;
	case "exg":
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = read(s, a, false);
		n = read(s, b, false);
		write(s, b, m);
		write(s, a, n);
		return;
	case "ext":
		switch(s){
		case "B": throw "404";
		case "W":
			a = _amode("B", x[2]);
			m = read("B", a, true);
			_nz("W", m);
			write("W", a, m);
			return;
		case "L":
			a = _amode("W", x[2]);
			m = read("W", a, true);
			_nz("L", m);
			write("L", a, m);
			return;
		default: throw "size";
		}
	case "jmp":
		a = _amode(s, x[2]);
		pc = (int)a;
		break;
	case "lea":
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		write(s, b, (int)a);
		return;
	case "link":
		a = _amode("L", x[2]);
		b = _amode("W", x[3]);
		rval_addr("L", 15);
		write("L", "R15", read("L", "R15", false) - 4);
		write("L", (string)read("L", "R15", false), read("L", a, false));
		write("L", a, read("L", "R15", false));
		write("L", "R15", read("L", "R15", false) + read("W", b, true));
		return;
	case "lsl":
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _lsl(s, read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "lsr":
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _lsr(s, read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "move":
		if(x[3][0] == "A" && s == "B") throw "404";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = read(s, a, false);
		_nz(s, m);
		write(s, b, m);
		return;
	case "movea":
		if(s == "B") throw "404";
		a = _amode(s, x[2]);
		b = _amode("L", x[3]);
		m = read(s, a, true);
		write("L", b, m);
		return;
	case "move_to_ccr":
		b = _amode(s, x[2]);
		_setccr(read(s, b, false));
		return;
	case "move_from_sr":
		b = _amode(s, x[2]);
		write("W", b, _rSstart);
		return;
	case "movem":
		if(s == "B") throw "404";
		pc += 2;
		if(x[2][0] == "list")
			_movem_store(s, x[3], x[2][1]);
		else
			_movem_load(s, x[2], x[3][1]);
		return;
	case "movep":
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		if(a[0] == "R"){
			m = read(s, a, false);
			switch(s){
			case "B": throw "no";
			case "W":
				write("B", b, m >> 8);
				write("B", (string)((int)b+2), m);
				break;
			case "L":
				write("B", b, m >> 24);
				write("B", (string)((int)b+2), m >> 16);
				write("B", (string)((int)b+4), m >> 8);
				write("B", (string)((int)b+6), m);
				break;
			}
		}else{
			m = read(s, a, false);
			switch(s){
			case "B": throw "no";
			case "W":
				mem_sug("B", ((int)a+2), random(8));
				m = read("B", a, false) << 8;
				m |= read("B", (string)((int)a+2), false);
				write("W", b, m);
				break;
			case "L":
				mem_sug("B", ((int)a+2), random(8));
				mem_sug("B", ((int)a+4), random(8));
				mem_sug("B", ((int)a+6), random(8));
				m = read("B", a, false) << 24;
				m |= read("B", (string)((int)a+2), false) << 16;
				m |= read("B", (string)((int)a+4), false) << 8;
				m |= read("B", (string)((int)a+6), false);
				write("L", b, m);
				break;
			}
		}
		break;
	case "moveq":
		b = _amode("L", x[3]);
		m = _s8(x[2][1]);
		_nz("B", m);
		write("L", b, m);
		break;
	case "muls":
		a = _amode("W", x[2]);
		b = _amode("L", x[3]);
		m = _nz("L", read("W", a, true) * read("W", b, true));
		write("L", b, m);
		break;
	case "mulu":
		a = _amode("W", x[2]);
		b = _amode("L", x[3]);
		m = _nz("L", read("W", a, false) * read("W", b, false));
		write("L", b, m);
		break;
	case "nbcd":
		a = _amode(s, x[2]);
		m = _nbcd(read(s, a, false));
		write(s, a, m);
		break;
	case "neg":
		a = _amode(s, x[2]);
		write(s, a, _sub(s, 0, read(s, a, true), 0, true, true));
		break;
	case "negx":
		a = _amode(s, x[2]);
		write(s, a, _sub(s, 0, read(s, a, true), (int)_x, true, false));
		break;
	case "nop":
		break;
	case "not":
		a = _amode(s, x[2]);
		write(s, a, _nz(s, ~read(s, a, true)));
		break;
	case "or":
	case "ori":
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _nz(s, read(s, b, false) | read(s, a, false));
		write(s, b, m);
		return;
	case "ori_ccr":
		_setccr(_getccr() | x[2][1]);
		pc += 2;
		return;
	case "pea":
		write("L", "R15", read("L", "R15", false) - 4);
		a = _amode(s, x[2]);
		rval_addr("L", 15);
		write("L", (string)read("L", "R15", false), (int)a);
		break;
	case "rol":
		a = _amode("B", x[2]);
		b = _amode(s, x[3]);
		m = _rol(s, read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "ror":
		a = _amode("B", x[2]);
		b = _amode(s, x[3]);
		m = _ror(s, read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "roxl":
		a = _amode("B", x[2]);
		b = _amode(s, x[3]);
		m = _roxl(s, read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "roxr":
		a = _amode("B", x[2]);
		b = _amode(s, x[3]);
		m = _roxr(s, read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "rtr":
		m = rval_addr("L", 15);
		mem_sug("W", m, random(32) & ~1);
		mem_sug("L", m+2, random(32) & ~1);
		_setccr(read("W", (string)m, false));
		pc = read("L", (string)_u32(m+2), false);
		write("L", "R15", m + 6);
		return;
	case "rts":
		m = rval_addr("L", 15);
		mem_sug("L", m, random(32) & ~1);
		pc = read("L", (string)m, false);
		write("L", "R15", m + 4);
		return;
	case "sbcd":
		if(x[2][0] == "-(A)" && x[2][1] == x[3][1]) throw "no";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _sbcd(read(s, b, false), read(s, a, false));
		write(s, b, m);
		return;
	case "scc":
		a = _amode(s, x[3]);
		write(s, a, _checkcc(x[2]) ? -1 : 0);
		return;
	case "sub":
	case "subi":
		if(x[3][0] == "A" && s == "B") throw "404";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _sub(s, read(s, b, false), read(s, a, false), 0, true, true);
		write(s, b, m);
		return;
	case "suba":
		if(s == "B") throw "404";
		a = _amode(s, x[2]);
		b = _amode("L", x[3]);
		m = read("L", b, false) - read(s, a, true);
		write("L", b, m);
		return;
	case "subq":
		x[2][1] &= 7;
		if(x[2][1] == 0) x[2][1] = 8;
		if(x[3][0] == "A"){
			b = _amode("L", x[3]);
			m = read("L", b, false) - x[2][1];
			write("L", b, m);
		}else{
			b = _amode(s, x[3]);
			m = _sub(s, read(s, b, false), x[2][1], 0, true, true);
			write(s, b, m);
		}
		return;
	case "subx":
		if(x[2][0] == "-(A)" && x[2][1] == x[3][1]) throw "no";
		a = _amode(s, x[2]);
		b = _amode(s, x[3]);
		m = _sub(s, read(s, b, false), read(s, a, false), (int)_x, true, false);
		write(s, b, m);
		return;
	case "swap":
		a = _amode("L", x[2]);
		m = read("L", a, false);
		m = m >>> 16 | m << 16;
		_nz("L", m);
		write("L", a, m);
		break;
	case "tas":
		a = _amode(s, x[2]);
		m = read(s, a, false);
		_nz(s, m);
		write(s, a, m | 0x80);
		break;
	case "tst":
		a = _amode(s, x[2]);
		m = read(s, a, false);
		_nz(s, m);
		break;
	case "unlk":
		rval_addr("L", 8+x[2][1]);
		a = _amode(s, x[2]);
		mem_sug("L", read("L", a, false), random(32) & ~1);
		write("L", "R15", read("L", a, false));
		write("L", a, read("L", (string)read("L", "R15", false), false));
		write("L", "R15", read("L", "R15", false) + 4);
		return;
	default:
		throw "dunno how to simulate " + (string)x;
	}
}

int
_swap6(int a)
{
	return (a << 3 | a >> 3) & 077;
}
var s0[string] = {"B": 0, "W": 1, "L": 2};
var s1[string] = {"W": 3, "L": 7};
var s2[string] = {"B": 1, "W": 3, "L": 2};
var s3[string] = {"W": 0, "L": 1};
var cc[string] = {"t": 0, "f": 1, "hi": 2, "ls": 3, "cc": 4, "cs": 5, "ne": 6, "eq": 7, "vc": 8, "vs": 9, "pl": 10, "mi": 11, "ge": 12, "lt": 13, "gt": 14, "le": 15};
var encodetab[] = [
	["abcd", "B", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xC100 | c<<9 | b],
	["abcd", "B", ["-(A)", "$1", "$"], ["-(A)", "$2", "$"]], var(a, b, c) return [0xC108 | c<<9 | b],
	["add", "$0", "$2", ["D", "$1", "$"]], var(a, b, c) return [0xD000 | s0[a]<<6 | b<<9 | encamode(c,"")].concat(amrest(a,c)),
	["add", "$0", ["D", "$1", "$"], "$2"], var(a, b, c) return [0xD100 | s0[a]<<6 | b<<9 | encamode(c,"ma")].concat(amrest(a,c)),
	["adda", "$0", "$2", ["A", "$1", "$"]], var(a, b, c) return [0xD000 | s1[a]<<6 | b<<9 | encamode(c,"")].concat(amrest(a,c)),
	["addi", "$0", ["#imm", "$1"], "$2"], var(a, b, c) return [0x0600 | s0[a]<<6 | encamode(c,"da")].concat(amrest(a,["#imm",b])).concat(amrest(a,c)),
	["addq", "$0", ["#imm8", "$1"], "$2"], var(a, b, c) return [0x5000 | s0[a]<<6 | (b & 7) << 9 | encamode(c,"a")].concat(amrest(a,c)),
	["addx", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xD100 | s0[a] << 6 | c<<9 | b],
	["addx", "$0", ["-(A)", "$1", "$"], ["-(A)", "$2", "$"]], var(a, b, c) return [0xD108 | s0[a] << 6 | c<<9 | b],
	["and", "$0", "$2", ["D", "$1", "$"]], var(a, b, c) return [0xC000 | s0[a]<<6 | b<<9 | encamode(c,"d")].concat(amrest(a,c)),
	["and", "$0", ["D", "$1", "$"], "$2"], var(a, b, c) return [0xC100 | s0[a]<<6 | b<<9 | encamode(c,"ma")].concat(amrest(a,c)),
	["andi", "$0", ["#imm", "$1"], "$2"], var(a, b, c) return [0x0200 | s0[a]<<6 | encamode(c,"da")].concat(amrest(a,["#imm",b])).concat(amrest(a,c)),
	["andi_ccr", "B", ["#imm", "$0"]], var(a) return [0x023C, a],
	["asl", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xe120 | s0[a] << 6 | b << 9 | c],
	["asl", "$0", ["#imm8", "$1"], ["D", "$2", "$"]], var(a, b, c) return [0xe100 | s0[a] << 6 | (b&7) << 9 | c],
	["asl", "W", ["#imm8", 1], "$0"], var(a) return [0xe1c0 | encamode(a,"ma")].concat(amrest("W",a)),
	["asr", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xe020 | s0[a] << 6 | b << 9 | c],
	["asr", "$0", ["#imm8", "$1"], ["D", "$2", "$"]], var(a, b, c) return [0xe000 | s0[a] << 6 | (b&7) << 9 | c],
	["asr", "W", ["#imm8", 1], "$0"], var(a) return [0xe0c0 | encamode(a,"ma")].concat(amrest("W",a)),
	["bcc", "$0", "$1"], var(a, b) return [0x6000 | cc[a] << 8 | encamode(b,"")].concat(amrest("W",b)),
	["bchg", "L", ["#imm", "$0"], ["D", "$1", "$"]], var(a, b) return [0x0840 | b, a],
	["bchg", "L", ["D", "$0", "$"], ["D", "$1", "$"]], var(a, b) return [0x0140 | a << 9 | b],
	["bchg", "B", ["#imm", "$0"], "$1"], var(a, b) return [0x0840 | encamode(b, "ma"), a].concat(amrest("B",b)),
	["bchg", "B", ["D", "$0", "$"], "$1"], var(a, b) return [0x0140 | a << 9 | encamode(b, "ma")].concat(amrest("B",b)),
	["bclr", "L", ["#imm", "$0"], ["D", "$1", "$"]], var(a, b) return [0x0880 | b, a],
	["bclr", "L", ["D", "$0", "$"], ["D", "$1", "$"]], var(a, b) return [0x0180 | a << 9 | b],
	["bclr", "B", ["#imm", "$0"], "$1"], var(a, b) return [0x0880 | encamode(b, "ma"), a].concat(amrest("B",b)),
	["bclr", "B", ["D", "$0", "$"], "$1"], var(a, b) return [0x0180 | a << 9 | encamode(b, "ma")].concat(amrest("B",b)),
	["bset", "L", ["#imm", "$0"], ["D", "$1", "$"]], var(a, b) return [0x08c0 | b, a],
	["bset", "L", ["D", "$0", "$"], ["D", "$1", "$"]], var(a, b) return [0x01c0 | a << 9 | b],
	["bset", "B", ["#imm", "$0"], "$1"], var(a, b) return [0x08c0 | encamode(b, "ma"), a].concat(amrest("B",b)),
	["bset", "B", ["D", "$0", "$"], "$1"], var(a, b) return [0x01c0 | a << 9 | encamode(b, "ma")].concat(amrest("B",b)),
	["bsr", "$0"], var(a) return [0x6100 | encamode(a,"")].concat(amrest("W",a)),
	["btst", "L", ["#imm", "$0"], ["D", "$1", "$"]], var(a, b) return [0x0800 | b, a],
	["btst", "L", ["D", "$0", "$"], ["D", "$1", "$"]], var(a, b) return [0x0100 | a << 9 | b],
	["btst", "B", ["#imm", "$0"], "$1"], var(a, b) return [0x0800 | encamode(b, "ma"), a].concat(amrest("B",b)),
	["btst", "B", ["D", "$0", "$"], "$1"], var(a, b) return [0x0100 | a << 9 | encamode(b, "ma")].concat(amrest("B",b)),
	["chk", "W", "$0", ["D", "$1", "$"]], var(a, b) return [0x4180 | b << 9 | encamode(a, "da")].concat(amrest("W",a)),
	["clr", "$0", "$1"], var(a, b) return [0x4200 | s0[a] << 6 | encamode(b, "da")].concat(amrest(a,b)),
	["cmp", "$0", "$2", ["D", "$1", "$"]], var(a, b, c) return [0xB000 | s0[a]<<6 | b<<9 | encamode(c,"")].concat(amrest(a,c)),
	["cmpa", "$0", "$2", ["A", "$1", "$"]], var(a, b, c) return [0xB000 | s1[a]<<6 | b<<9 | encamode(c,"")].concat(amrest(a,c)),
	["cmpi", "$0", ["#imm", "$1"], "$2"], var(a, b, c) return [0x0C00 | s0[a]<<6 | encamode(c,"da")].concat(amrest(a,["#imm",b])).concat(amrest(a,c)),
	["cmpm", "$0", ["(A)+", "$1", "$"], ["(A)+", "$2", "$"]], var(a, b, c) return [0xB108 | s0[a]<<6 | c << 9 | b],
	["dbcc", "$0", ["D", "$1", "$"], ["#imm", "$2"]], var(a, b, c) return [0x50C8 | cc[a] << 8 | b, c],
	["divs", "W", "$0", ["D", "$1", "$"]], var(a, b) return [0x81C0 | b<<9 | encamode(a, "d")].concat(amrest("W", a)),
	["divu", "W", "$0", ["D", "$1", "$"]], var(a, b) return [0x80C0 | b<<9 | encamode(a, "d")].concat(amrest("W", a)),
	["eor", "$0", ["D", "$1", "$"], "$2"], var(a, b, c) return [0xB100 | s0[a]<<6 | b<<9 | encamode(c,"ma")].concat(amrest(a,c)),
	["eori", "$0", ["#imm", "$1"], "$2"], var(a, b, c) return [0x0A00 | s0[a]<<6 | encamode(c,"da")].concat(amrest(a,["#imm",b])).concat(amrest(a,c)),
	["eori_ccr", "B", ["#imm", "$0"]], var(a) return [0x0A3C, a],
	["exg", "L", ["D", "$0", "$"], ["D", "$1", "$"]], var(a,b) return [0xC140 | a<<9 | b],
	["exg", "L", ["D", "$0", "$"], ["A", "$1", "$"]], var(a,b) return [0xC188 | a<<9 | b],
	["exg", "L", ["A", "$0", "$"], ["A", "$1", "$"]], var(a,b) return [0xC148 | a<<9 | b],
	["ext", "$0", ["D", "$1", "$"]], var(a,b) return [0x4800 | s0[a]+1<<6 | b],
	["jmp", "W", "$0"], var(a) return [0x4EC0 | encamode(a, "c")].concat(amrest("W",a)),
	["lea", "L", "$0", ["A", "$1", "$"]], var(a,b) return [0x41C0 | b<<9 | encamode(a, "c")].concat(amrest("L",a)),
	["link", "W", ["A", "$0", "$"], ["#imm", "$1"]], var(a,b) return [0x4E50 | a, b],
	["lsl", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xe128 | s0[a] << 6 | b << 9 | c],
	["lsl", "$0", ["#imm8", "$1"], ["D", "$2", "$"]], var(a, b, c) return [0xe108 | s0[a] << 6 | (b&7) << 9 | c],
	["lsl", "W", ["#imm8", 1], "$0"], var(a) return [0xe3c0 | encamode(a,"ma")].concat(amrest("W",a)),
	["lsr", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xe028 | s0[a] << 6 | b << 9 | c],
	["lsr", "$0", ["#imm8", "$1"], ["D", "$2", "$"]], var(a, b, c) return [0xe008 | s0[a] << 6 | (b&7) << 9 | c],
	["lsr", "W", ["#imm8", 1], "$0"], var(a) return [0xe2c0 | encamode(a,"ma")].concat(amrest("W",a)),
	["move", "$0", "$1", "$2"], var(a,b,c) return [s2[a]<<12 | _swap6(encamode(c,"da"))<<6 | encamode(b,"")].concat(amrest(a,b)).concat(amrest(a,c)),
	["movea", "$0", "$1", ["A", "$2", "$"]], var(a,b,c) return [s2[a]<<12 | c<<9 | 1<<6 | encamode(b,"")].concat(amrest(a,b)),
	["move_to_ccr", "W", "$0"], var(a) return [0x44C0 | encamode(a, "d")].concat(amrest("W", a)),
	["move_from_sr", "W", "$0"], var(a) return [0x40C0 | encamode(a, "da")].concat(amrest("W", a)),
	["movem", "$0", ["list", "$1"], ["-(A)", "$2", "$3"]], var(a,b,c,d) return [0x4880 | s3[a] << 6 | encamode(["-(A)", c, d], ""), b].concat(amrest(a, ["-(A)", c, d])),
	["movem", "$0", ["list", "$1"], "$2"], var(a,b,c) return [0x4880 | s3[a] << 6 | encamode(c, "c"), b].concat(amrest(a, c)),
	["movem", "$0", ["(A)+", "$2", "$3"], ["list", "$1"]], var(a,b,c,d) return [0x4C80 | s3[a] << 6 | encamode(["(A)+", c, d], ""), b].concat(amrest(a, ["(A)+", c, d])),
	["movem", "$0", "$2", ["list", "$1"]], var(a,b,c) return [0x4C80 | s3[a] << 6 | encamode(c, "c"), b].concat(amrest(a, c)),
	["movep", "$0", ["D", "$1", "$"], ["(d16,A)", "$2", "$3", "$"]], var(a, b, c, d) return [0x0188 | s3[a]<<6 | b<<9 | c, d],
	["movep", "$0", ["(d16,A)", "$2", "$3", "$"], ["D", "$1", "$"]], var(a, b, c, d) return [0x0108 | s3[a]<<6 | b<<9 | c, d],
	["moveq", "L", ["#imm", "$0"], ["D", "$1", "$"]], var(a,b) return [0x7000 | b<<9 | _u8(a)],
	["muls", "W", "$0", ["D", "$1", "$"]], var(a, b) return [0xC1C0 | b<<9 | encamode(a, "da")].concat(amrest("W", a)),
	["mulu", "W", "$0", ["D", "$1", "$"]], var(a, b) return [0xC0C0 | b<<9 | encamode(a, "da")].concat(amrest("W", a)),
	["nbcd", "B", "$0"], var(a) return [0x4800 | encamode(a, "da")].concat(amrest("W", a)),
	["neg", "$0", "$1"], var(a, b) return [0x4400 | s0[a] << 6 | encamode(b, "da")].concat(amrest(a, b)),
	["negx", "$0", "$1"], var(a, b) return [0x4000 | s0[a] << 6 | encamode(b, "da")].concat(amrest(a, b)),
	["nop"], var() return [0x4E71],
	["not", "$0", "$1"], var(a, b) return [0x4600 | s0[a] << 6 | encamode(b, "da")].concat(amrest(a, b)),
	["or", "$0", "$2", ["D", "$1", "$"]], var(a, b, c) return [0x8000 | s0[a]<<6 | b<<9 | encamode(c,"")].concat(amrest(a,c)),
	["or", "$0", ["D", "$1", "$"], "$2"], var(a, b, c) return [0x8100 | s0[a]<<6 | b<<9 | encamode(c,"ma")].concat(amrest(a,c)),
	["ori", "$0", ["#imm", "$1"], "$2"], var(a, b, c) return [0x0000 | s0[a]<<6 | encamode(c,"da")].concat(amrest(a,["#imm",b])).concat(amrest(a,c)),
	["ori_ccr", "B", ["#imm", "$0"]], var(a) return [0x003C, a],
	["pea", "L", "$0"], var(a) return [0x4840 | encamode(a, "c")].concat(amrest("L",a)),
	["rol", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xe138 | s0[a] << 6 | b << 9 | c],
	["rol", "$0", ["#imm8", "$1"], ["D", "$2", "$"]], var(a, b, c) return [0xe118 | s0[a] << 6 | (b&7) << 9 | c],
	["rol", "W", ["#imm8", 1], "$0"], var(a) return [0xe7c0 | encamode(a,"ma")].concat(amrest("W",a)),
	["ror", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xe038 | s0[a] << 6 | b << 9 | c],
	["ror", "$0", ["#imm8", "$1"], ["D", "$2", "$"]], var(a, b, c) return [0xe018 | s0[a] << 6 | (b&7) << 9 | c],
	["ror", "W", ["#imm8", 1], "$0"], var(a) return [0xe6c0 | encamode(a,"ma")].concat(amrest("W",a)),
	["roxl", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xe130 | s0[a] << 6 | b << 9 | c],
	["roxl", "$0", ["#imm8", "$1"], ["D", "$2", "$"]], var(a, b, c) return [0xe110 | s0[a] << 6 | (b&7) << 9 | c],
	["roxl", "W", ["#imm8", 1], "$0"], var(a) return [0xe5c0 | encamode(a,"ma")].concat(amrest("W",a)),
	["roxr", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0xe030 | s0[a] << 6 | b << 9 | c],
	["roxr", "$0", ["#imm8", "$1"], ["D", "$2", "$"]], var(a, b, c) return [0xe010 | s0[a] << 6 | (b&7) << 9 | c],
	["roxr", "W", ["#imm8", 1], "$0"], var(a) return [0xe4c0 | encamode(a,"ma")].concat(amrest("W",a)),
	["rtr"], var(a) return [0x4E77],
	["rts"], var(a) return [0x4E75],
	["sbcd", "B", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0x8100 | c<<9 | b],
	["sbcd", "B", ["-(A)", "$1", "$"], ["-(A)", "$2", "$"]], var(a, b, c) return [0x8108 | c<<9 | b],
	["scc", "B", "$0", "$1"], var(a, b) return [0x50C0 | cc[a] << 8 | encamode(b,"da")].concat(amrest("W",b)),
	["sub", "$0", "$2", ["D", "$1", "$"]], var(a, b, c) return [0x9000 | s0[a]<<6 | b<<9 | encamode(c,"")].concat(amrest(a,c)),
	["sub", "$0", ["D", "$1", "$"], "$2"], var(a, b, c) return [0x9100 | s0[a]<<6 | b<<9 | encamode(c,"ma")].concat(amrest(a,c)),
	["suba", "$0", "$2", ["A", "$1", "$"]], var(a, b, c) return [0x9000 | s1[a]<<6 | b<<9 | encamode(c,"")].concat(amrest(a,c)),
	["subi", "$0", ["#imm", "$1"], "$2"], var(a, b, c) return [0x0400 | s0[a]<<6 | encamode(c,"da")].concat(amrest(a,["#imm",b])).concat(amrest(a,c)),
	["subq", "$0", ["#imm8", "$1"], "$2"], var(a, b, c) return [0x5100 | s0[a]<<6 | (b & 7) << 9 | encamode(c,"a")].concat(amrest(a,c)),
	["subx", "$0", ["D", "$1", "$"], ["D", "$2", "$"]], var(a, b, c) return [0x9100 | s0[a] << 6 | c<<9 | b],
	["subx", "$0", ["-(A)", "$1", "$"], ["-(A)", "$2", "$"]], var(a, b, c) return [0x9108 | s0[a] << 6 | c<<9 | b],
	["swap", "W", ["D", "$0", "$"]], var(a) return [0x4840 | a],
	["tas", "B", "$0"], var(a) return [0x4AC0 | encamode(a, "da")].concat(amrest("B", a)),
	["tst", "$0", "$1"], var(a, b) return [0x4A00 | s0[a]<<6 | encamode(b, "")].concat(amrest(a, b)),
	["unlk", "L", ["A", "$0", "$"]], var(a,b) return [0x4E58 | a],
];

void
test(void)
{
	int j, k, l;
	var amodes[] = [
		["D", "%r3", "%R32"],
		["A", "%r3", "%R32"],
		["(A)", "%r3", "%R32"],
		["(A)+", "%r3", "%R32"],
		["-(A)", "%r3", "%R32"],
		["(d16,A)", "%r3", "%R16", "%R32"],
		["(d8,A,X.W)", "%r3", "%r4", "%r8", "%R32"],
		["(d8,A,X.L)", "%r3", "%r4", "%r8", "%R32"],
		["(d16,PC)", "%r3", "%R16", "%R32"],
		["(d8,PC,X.W)", "%r4", "%r8", "%R32"],
		["(d8,PC,X.L)", "%r4", "%r8", "%R32"],
		["(xxx).W", "%r16", "%R32"],
		["(xxx).L", "%r32", "%R32"],
		["#imm", "%R32"],
	];
	string ccs[] = ["t", "f", "hi", "ls", "cc", "cs", "ne", "eq", "vc", "vs", "pl", "mi", "ge", "lt", "gt", "le"];
	var Dn = amodes[0];
	var An = amodes[1];
	var Imm = ["#imm", "%R32"];
	var Imm8 = ["#imm8", "%s8"];
	
	run(["abcd", "B", Dn, Dn]);
	run(["abcd", "B", ["-(A)", "%r3", "%R32"], ["-(A)", "%r3", "%R32"]]);
	run(["andi_ccr", "B", ["#imm", "%R16"]]);
	run(["bchg", "L", ["#imm", "%r16"], Dn]);
	run(["bchg", "L", Dn, Dn]);
	run(["bclr", "L", ["#imm", "%r16"], Dn]);
	run(["bclr", "L", Dn, Dn]);
	run(["bset", "L", ["#imm", "%r16"], Dn]);
	run(["bset", "L", Dn, Dn]);
	run(["btst", "L", ["#imm", "%r16"], Dn]);
	run(["btst", "L", Dn, Dn]);
	run(["cmpm", "B", ["(A)+", "%r3", "%R32"], ["(A)+", "%r3", "%R32"]]);
	run(["eori_ccr", "B", ["#imm", "%R16"]]);
	run(["exg", "L", Dn, Dn]);
	run(["exg", "L", Dn, An]);
	run(["exg", "L", An, An]);
	run(["link", "W", An, ["#imm", "%R16"]]);
	run(["moveq", "L", ["#imm", "%R8"], Dn]);
	run(["ori_ccr", "B", ["#imm", "%R16"]]);
	run(["sbcd", "B", Dn, Dn]);
	run(["sbcd", "B", ["-(A)", "%r3", "%R32"], ["-(A)", "%r3", "%R32"]]);
	run(["swap", "W", Dn]);
	run(["unlk", "L", An]);
	for(j = 0; j < 3; j++){
		run(["addx", "BWL".substr(j,1), Dn, Dn]);
		run(["addx", "BWL".substr(j,1), ["-(A)", "%r3", "%R32"], ["-(A)", "%r3", "%R32"]]);
		run(["asl", "BWL".substr(j,1), Imm8, Dn]);
		run(["asl", "BWL".substr(j,1), Dn, Dn]);
		run(["asr", "BWL".substr(j,1), Imm8, Dn]);
		run(["asr", "BWL".substr(j,1), Dn, Dn]);	
		run(["ext", "BWL".substr(j,1), Dn]);		
		run(["lsl", "BWL".substr(j,1), Imm8, Dn]);
		run(["lsl", "BWL".substr(j,1), Dn, Dn]);
		run(["lsr", "BWL".substr(j,1), Imm8, Dn]);
		run(["lsr", "BWL".substr(j,1), Dn, Dn]);	
		run(["movep", "BWL".substr(j,1), Dn, ["(d16,A)", "%r3", "%r16", "%R32"]]);
		run(["movep", "BWL".substr(j,1), ["(d16,A)", "%r3", "%r16", "%R32"], Dn]);
		run(["rol", "BWL".substr(j,1), Imm8, Dn]);
		run(["rol", "BWL".substr(j,1), Dn, Dn]);	
		run(["ror", "BWL".substr(j,1), Imm8, Dn]);
		run(["ror", "BWL".substr(j,1), Dn, Dn]);	
		run(["roxl", "BWL".substr(j,1), Imm8, Dn]);
		run(["roxl", "BWL".substr(j,1), Dn, Dn]);	
		run(["roxr", "BWL".substr(j,1), Imm8, Dn]);
		run(["roxr", "BWL".substr(j,1), Dn, Dn]);	
		run(["subx", "BWL".substr(j,1), Dn, Dn]);
		run(["subx", "BWL".substr(j,1), ["-(A)", "%r3", "%R32"], ["-(A)", "%r3", "%R32"]]);
		for(k = 0; k < amodes.length; k++){
			run(["add", "BWL".substr(j,1), Dn, amodes[k]]);
			run(["add", "BWL".substr(j,1), amodes[k], Dn]);
			run(["adda", "BWL".substr(j,1), amodes[k], An]);
			run(["addi", "BWL".substr(j,1), Imm, amodes[k]]);
			run(["addq", "BWL".substr(j,1), Imm8, amodes[k]]);
			run(["and", "BWL".substr(j,1), Dn, amodes[k]]);
			run(["and", "BWL".substr(j,1), amodes[k], Dn]);
			run(["andi", "BWL".substr(j,1), Imm, amodes[k]]);
			run(["clr", "BWL".substr(j,1), amodes[k]]);
			run(["cmp", "BWL".substr(j,1), amodes[k], Dn]);
			run(["cmpa", "BWL".substr(j,1), amodes[k], An]);
			run(["cmpi", "BWL".substr(j,1), Imm, amodes[k]]);
			run(["eor", "BWL".substr(j,1), Dn, amodes[k]]);
			run(["eori", "BWL".substr(j,1), Imm, amodes[k]]);
			run(["movea", "BWL".substr(j,1), amodes[k], An]);
			for(l = 0; l < amodes.length; l++)
				run(["move", "BWL".substr(j, 1), amodes[k], amodes[l]]);
			run(["movem", "BWL".substr(j,1), ["list", "%R16"], amodes[k]]);
			run(["movem", "BWL".substr(j,1), amodes[k], ["list", "%R16"]]);
			run(["neg", "BWL".substr(j,1), amodes[k]]);
			run(["negx", "BWL".substr(j,1), amodes[k]]);
			run(["not", "BWL".substr(j,1), amodes[k]]);
			run(["or", "BWL".substr(j,1), Dn, amodes[k]]);
			run(["ori", "BWL".substr(j,1), Imm, amodes[k]]);
			run(["sub", "BWL".substr(j,1), Dn, amodes[k]]);
			run(["sub", "BWL".substr(j,1), amodes[k], Dn]);
			run(["suba", "BWL".substr(j,1), amodes[k], An]);
			run(["subi", "BWL".substr(j,1), Imm, amodes[k]]);
			run(["subq", "BWL".substr(j,1), Imm8, amodes[k]]);
			run(["tst", "BWL".substr(j,1), amodes[k]]);
		}
	}
	for(k = 0; k < amodes.length; k++){
		run(["asl", "W", ["#imm8", 1], amodes[k]]);
		run(["asr", "W", ["#imm8", 1], amodes[k]]);
		run(["bchg", "B", Imm, amodes[k]]);
		run(["bchg", "B", Dn, amodes[k]]);
		run(["bclr", "B", Imm, amodes[k]]);
		run(["bclr", "B", Dn, amodes[k]]);
		run(["bset", "B", Imm, amodes[k]]);
		run(["bset", "B", Dn, amodes[k]]);
		run(["btst", "B", Imm, amodes[k]]);
		run(["btst", "B", Dn, amodes[k]]);
//		run(["chk", "W", amodes[k], Dn]);
		run(["divs", "W", amodes[k], Dn]);
		run(["divu", "W", amodes[k], Dn]);
		run(["jmp", "W", amodes[k]]);
		run(["lea", "L", amodes[k], An]);
		run(["move_to_ccr", "W", amodes[k]]);
		run(["move_from_sr", "W", amodes[k]]);
		run(["muls", "W", amodes[k], Dn]);
		run(["mulu", "W", amodes[k], Dn]);
		run(["nbcd", "B", amodes[k]]);
		run(["pea", "L", amodes[k]]);
		run(["rol", "W", ["#imm8", 1], amodes[k]]);
		run(["ror", "W", ["#imm8", 1], amodes[k]]);
		run(["roxl", "W", ["#imm8", 1], amodes[k]]);
		run(["roxr", "W", ["#imm8", 1], amodes[k]]);
		run(["tas", "B", amodes[k]]);
	}
	run(["nop"]);
	run(["rtr"]);
	run(["rts"]);
	for(j = 0; j < 16; j++){
		if(ccs[j] != "f")
			run(["bcc", ccs[j], ["#disp", "%R32"]]);
		run(["dbcc", ccs[j], Dn, ["#imm", "%R16"]]);
		for(k = 0; k < amodes.length; k++)
			run(["scc", "B", ccs[j], amodes[k]]);
	}
	run(["bsr", ["#disp", "%R32"]]);
}

void
tests(void)
{
	int i;
	
	for(i = 0; i < 10; i++)
		test();
}

/*
	missing:
	CHK
	ILLEGAL
	TRAP*
*/
